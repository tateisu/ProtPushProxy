import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
}

android {
    namespace "jp.juggler.pushreceiverapp"
    compileSdk 33

    defaultConfig {
        applicationId "jp.juggler.pushreceiverapp"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    // Specifies one flavor dimension.
    flavorDimensions "fcm"
    productFlavors {
        nofcm {
            dimension "fcm"
            // applicationIdSuffix ".demo"
            versionNameSuffix "-nofcm"
        }
        fcm {
            dimension "fcm"
            // applicationIdSuffix ".full"
            versionNameSuffix "-fcm"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.drawerlayout:drawerlayout:1.1.1"
    implementation "androidx.emoji2:emoji2-bundled:$emoji2Version"
    implementation "androidx.emoji2:emoji2-views-helper:$emoji2Version"
    implementation "androidx.emoji2:emoji2-views:$emoji2Version"
    implementation "androidx.emoji2:emoji2:$emoji2Version"
    implementation "androidx.exifinterface:exifinterface:1.3.5"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.startup:startup-runtime:$startupVersion"
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation "com.astuetz:pagerslidingtabstrip:1.0.1"
    implementation "com.caverock:androidsvg-aar:1.4"
    implementation "com.github.UnifiedPush:android-connector:2.1.1"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.hadilq:live-event:1.3.0"
    implementation "com.github.omadahealth:swipy:1.2.3@aar"
    implementation "com.github.penfeizhou.android.animation:awebp:2.24.0"
    implementation "com.github.penfeizhou.android.animation:glide-plugin:2.24.0"
    implementation "com.github.woxthebox:draglistview:1.6.6"
    implementation "com.google.android.flexbox:flexbox:3.0.0"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.google.firebase:firebase-messaging:23.1.1"
    implementation "com.otaliastudios:transcoder:0.10.4"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "commons-codec:commons-codec:1.15"
    implementation "org.bouncycastle:bcpkix-jdk15on:1.70"
    implementation "org.conscrypt:conscrypt-android:2.5.2"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinxCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
    implementation "ru.gildor.coroutines:kotlin-coroutines-okhttp:1.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    fcmImplementation "com.google.firebase:firebase-messaging:23.1.1"
    fcmImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$kotlinxCoroutinesVersion"

    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarLibVersion"
}

def willApplyGoogleService() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    Matcher matcher

    matcher = Pattern.compile("assemble|generate", Pattern.CASE_INSENSITIVE).matcher(tskReqStr)
    if (!matcher.find()) {
        // not assemble or generate task.
        return false
    }

    matcher = Pattern.compile("(?:assemble|generate)fcm\\w+", Pattern.CASE_INSENSITIVE).matcher(tskReqStr)
    if (!matcher.find()) {
        println "willApplyGoogleService=false. $tskReqStr"
        return false
    } else {
        println "willApplyGoogleService=true. $tskReqStr"
        return true
    }
}

if (willApplyGoogleService()) apply plugin: "com.google.gms.google-services"
